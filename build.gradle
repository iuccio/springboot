buildscript {

    ext {
        springBootVersion = '1.5.4.RELEASE'
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        // tag::build[]
        classpath('se.transmode.gradle:gradle-docker:1.2')
        // end::build[]
    }
}

plugins {
    id 'org.flywaydb.flyway' version '4.2.0'
    id 'org.springframework.boot' version '1.5.4.RELEASE'
    id 'java'
    id 'idea'
}

apply plugin: 'docker'

// This is used as the docker image prefix (org)
group = 'iuccio'

jar {
    baseName = 'springboot'
    version = '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-jersey'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'

    compile 'org.flywaydb:flyway-core:4.0.3'

    runtime 'org.postgresql:postgresql'

    testCompile "junit:junit"
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

flyway {

    def flyway = new Properties()
    file("flyway.properties").withInputStream { flyway.load(it) }

    url = flyway.url
    user = flyway.user
    password = flyway.password
    schemas = flyway.schemas
    locations = flyway.locations
}

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    applicationName = jar.baseName
    dockerfile = file('Dockerfile')
    doFirst {
        copy {
            from jar
            into "build/docker/target"
        }
    }
}
